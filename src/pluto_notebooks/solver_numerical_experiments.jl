### A Pluto.jl notebook ###
# v0.19.38

using Markdown
using InteractiveUtils

# ╔═╡ b407fd34-d0a6-11ee-3345-fd33e806845d
begin
    using Pkg
    NOTEBOOKS_PATH = "src/pluto_notebooks"
    DATA_PATH = "data"  # store generated by notebook data
    @assert endswith(pwd(), NOTEBOOKS_PATH) "Wrong directory!"
    RDR_PATH = "../.."  # path to the RDR project root
    Pkg.activate(RDR_PATH)

    import RansacDoneRight as RDR
    md"""
    ### Activating RansacDoneRight environment...
    """
end

# ╔═╡ dffabbf8-eb57-4853-b195-f9b8ae858ed8
begin
    using Revise
    using StaticArrays
    using LinearAlgebra
    using Plots
    using KernelDensity
    using StatsBase
    using Statistics
    using StatsPlots
end

# ╔═╡ 49761944-0e6d-4621-9d2f-26f41b8f326c
html"""
<style>
	main {
		margin: 0 auto;
		max-width: 2000px;
    	padding-left: max(160px, 15%);
    	padding-right: max(160px, 15%);
	}
</style>
"""

# ╔═╡ 3129c8ed-a339-4171-95fe-8cb9bd78525a
"""
- `solver` takes in the vector of correspondences and outputs `H` -- a `3 × 3` matrix.
- `n_runs` is the number of iterations for a numerical stability test.

The norms of the residuals are returned.
"""
function test_numerical_stability(solver::Function, n_runs)
    residuals = Vector{Float64}(undef, 4 * n_runs)

    counter = 1
    for _ in 1:n_runs
        minimal_set::MVector{4,RDR.Correspondence{Float64}} = RDR.generate_random_minimal_set()

        Ĥ = solver(minimal_set)
        lu_Ĥ = lu(Ĥ)

        for j in 1:4
            corresp = minimal_set[j]

            r::MVector{4,Float64} = RDR.compute_residual(Ĥ, lu_Ĥ, corresp)

            residuals[counter] = sqrt(r[1] * r[1] + r[2] * r[2] + r[3] * r[3] + r[4] * r[4])
            counter += 1
        end
    end
    residuals
end

# ╔═╡ 05ee4181-b3bd-42bf-87c6-8ac77d9bfc13
solver_errors = test_numerical_stability(RDR.compute_homography, 100_000)

# ╔═╡ 4ed7e102-80f3-4c58-ba85-4ddd4016f3ef
"""
plots smoothed histogram.
"""
function plot_smoothed_histogram(values::V) where {V<:AbstractVector{Float64}}
    hist = fit(Histogram, values, closed=:left)
    kde = KernelDensity.kde(values)

    plot(kde.x, kde.density, label="Smoothed Density", linewidth=2, size=(800, 600))
    histogram!(values, bins=hist.edges[1], alpha=0.5, label="Histogram", normed=true)
end

# ╔═╡ d7a9f07e-d85a-428d-856a-91f5f03eb594
begin
    plot_num_stability = plot_smoothed_histogram(log10.(filter(x -> x > 0, solver_errors))
    )
    xlabel!("Log10 Error (Residual)")
    ylabel!("Empirical Density")
    title!("Numerical Stability Experiment. Errors' Empirical Density")
    plot_num_stability
end

# ╔═╡ db084605-cf6a-46a5-9efc-e32d2dd6ec7f
"""
- `solver` takes in the vector of correspondences and outputs `H` -- a `3 × 3` matrix.
- `n_runs` is the number of iterations for a numerical stability test.

- `σ₁` is the std. dev. of the noise for the points in the first image.
- `σ₂` is the std. dev. of the noise for the points in the second image.

The norms of the residuals are returned.
"""
function test_numerical_sensitivity(solver::Function, σ₁, σ₂, n_minimal_sets, runs_for_min_set)
    residuals = Vector{Float64}(undef, 4 * n_minimal_sets * runs_for_min_set)

    noise_m₁ = [σ₁^2 0; 0 σ₁^2]
    noise_m₂ = [σ₂^2 0; 0 σ₂^2]

    counter = 1

    for _ in 1:n_minimal_sets
        minimal_set::MVector{4,RDR.Correspondence{Float64}} = RDR.generate_random_minimal_set()

        for k in 1:runs_for_min_set
            noised = RDR.add_noise(minimal_set, noise_m₁, noise_m₂, true)

            Ĥ = solver(noised)
            lu_Ĥ = lu(Ĥ)

            for j in 1:4
                corresp = minimal_set[j]

                r::MVector{4,Float64} = RDR.compute_residual(Ĥ, lu_Ĥ, corresp)

                residuals[counter] = sqrt(r[1] * r[1] + r[2] * r[2] + r[3] * r[3] + r[4] * r[4])
                counter += 1
            end
        end
    end
    residuals
end

# ╔═╡ 21c53c8d-4a45-4114-aec8-9472e9d9c124
begin
    sigmas = [0.001, 0.01, 0.1, 0.25, 1.0, 2.5]

    plots = []
    for sigma in sigmas
        errors = test_numerical_sensitivity(RDR.compute_homography, sigma, sigma, 50, 10_000)
        plot = plot_smoothed_histogram(log10.(filter(x -> x > 0, errors)))
        xlabel!("Log10 Norm of the Reprojection Residual")
        ylabel!("Density")
        title!("Std. Deviation of the noise: " * "$(sigma)")
        push!(plots, plot)
    end
end

# ╔═╡ b5b4c13b-6426-4a66-9cc0-ed4503b74c6d
plots

# ╔═╡ Cell order:
# ╟─49761944-0e6d-4621-9d2f-26f41b8f326c
# ╟─b407fd34-d0a6-11ee-3345-fd33e806845d
# ╠═dffabbf8-eb57-4853-b195-f9b8ae858ed8
# ╠═3129c8ed-a339-4171-95fe-8cb9bd78525a
# ╠═05ee4181-b3bd-42bf-87c6-8ac77d9bfc13
# ╟─4ed7e102-80f3-4c58-ba85-4ddd4016f3ef
# ╟─d7a9f07e-d85a-428d-856a-91f5f03eb594
# ╟─db084605-cf6a-46a5-9efc-e32d2dd6ec7f
# ╠═21c53c8d-4a45-4114-aec8-9472e9d9c124
# ╟─b5b4c13b-6426-4a66-9cc0-ed4503b74c6d
